/*
 * change mainClass
 * gradle build
 * java -jar build/libs/ComposeTools.jar
 */

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.6.RELEASE")
	}
}

apply plugin: 'java' 
apply plugin: 'eclipse' 
apply plugin: 'groovy'
apply plugin: 'spring-boot'

repositories {
	mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

springBoot {
	mainClass = "org.olabdynamics.compose.main.Main"
}

jar {
	baseName = 'ComposeTools'
	version =  'alpha-1.0'
}

dependencies {

	compile 'org.codehaus.groovy:groovy-all:2.4.5'
	
	compile("org.springframework.boot:spring-boot-starter-web:1.3.5.RELEASE")
	compile("org.springframework.boot:spring-boot-starter:1.3.5.RELEASE")
	compile("org.springframework:spring-tx")
	compile("org.springframework.boot:spring-boot-devtools:1.3.5.RELEASE")
	
	compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.21'
	
	compile group: 'com.sun.codemodel', name: 'codemodel', version: '2.6'
	
	compile 'org.springframework.integration:spring-integration-core:4.2.4.RELEASE'
	compile 'org.springframework.integration:spring-integration-file:4.2.4.RELEASE'
	compile 'org.springframework.integration:spring-integration-http:4.2.4.RELEASE'
	compile 'org.springframework.integration:spring-integration-jdbc:4.2.4.RELEASE'
	compile 'org.springframework.integration:spring-integration-event:4.2.4.RELEASE'
	compile 'org.springframework.integration:spring-integration-scripting:4.2.4.RELEASE'
	
	compile 'com.h2database:h2:1.3.175'
	
}

sourceSets {
	main {
		java {
			srcDirs = [ 'src/main/java' ]
			srcDir 'compose'
		}
		groovy {
			srcDirs = [ 'src/main/groovy' ]
			srcDir 'compose'
		}
	}
}

task compile(type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	main = 'org.olabdynamics.compose.tools.Compiler'
	if(project.hasProperty("composeFile")){
		args Eval.me(composeFile)
		//args project.getProperty('composeFile') + ' ' + project.getProperty('QoS')
	}	
}

task restClient(type: JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	main = 'org.olabdynamics.compose.main.RestWebServiceClientTest'
}